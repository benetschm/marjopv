Imports System.Data
Imports System.IO
Imports System.Data.SqlClient
Imports GlobalVariables
Imports GlobalCode

Partial Class Application_EditPatient
    Inherits System.Web.UI.Page

    Protected Sub Page_Load(sender As Object, e As EventArgs) Handles Me.Load
        If Page.IsPostBack = False Then
            ICSRID_HiddenField.Value = Request.QueryString("ICSRID")
            Dim CurrentICSR_ID As Integer = ICSRID_HiddenField.Value
            Title_Label.Text = "ICSR " & CurrentICSR_ID & ": Edit Patient"
            If Login_Status = True Then
                If CanEdit(tables.ICSRs, CurrentICSR_ID, tables.ICSRs, fields.Edit) = True Then 'If current user has the right to edit the current ICSR (which inlcudes the right to edit the patient)
                    'Format Controls
                    AtEditPageLoadButtonsFormat(Status_Label, SaveUpdates_Button, Nothing, Cancel_Button, ReturnToICSROverview_Button)
                    If CanEdit(tables.ICSRs, CurrentICSR_ID, tables.ICSRs, fields.Patient_Initials) = True Then
                        PatientInitials_TextBox.ReadOnly = False
                    Else
                        PatientInitials_TextBox.ReadOnly = True
                        PatientInitials_TextBox.ToolTip = CannotEditControlText
                    End If
                    If CanEdit(tables.ICSRs, CurrentICSR_ID, tables.ICSRs, fields.Patient_YearOfBirth_ID) = True Then
                        PatientYearOfBirth_DropDownList.Enabled = True
                    Else
                        PatientYearOfBirth_DropDownList.Enabled = False
                        PatientYearOfBirth_DropDownList.ToolTip = CannotEditControlText
                    End If
                    If CanEdit(tables.ICSRs, CurrentICSR_ID, tables.ICSRs, fields.Patient_Gender_ID) = True Then
                        PatientGender_DropDownList.Enabled = True
                    Else
                        PatientGender_DropDownList.Enabled = False
                        PatientGender_DropDownList.ToolTip = CannotEditControlText
                    End If
                    'Populate PatientYearOfBirth_DropDownList
                    PatientYearOfBirth_DropDownList.DataSource = CreateDropDownListDatatable(tables.YearsOfBirth)
                    PatientYearOfBirth_DropDownList.DataValueField = "ID"
                    PatientYearOfBirth_DropDownList.DataTextField = "Name"
                    PatientYearOfBirth_DropDownList.DataBind()
                    'Populate PatientGender_DropDownList
                    PatientGender_DropDownList.DataSource = CreateDropDownListDatatable(tables.Genders)
                    PatientGender_DropDownList.DataValueField = "ID"
                    PatientGender_DropDownList.DataTextField = "Name"
                    PatientGender_DropDownList.DataBind()
                    'Populate Patient Controls
                    Dim AtEditPageLoadPatientReadCommand As New SqlCommand("SELECT Patient_Initials, Patient_YearOfBirth_ID, Patient_Gender_ID FROM ICSRs WHERE ID = @CurrentICSR_ID", Connection)
                    AtEditPageLoadPatientReadCommand.Parameters.AddWithValue("@CurrentICSR_ID", CurrentICSR_ID)
                    Try
                        Connection.Open()
                        Dim AtEditPageLoadPatientReader As SqlDataReader = AtEditPageLoadPatientReadCommand.ExecuteReader()
                        While AtEditPageLoadPatientReader.Read()
                            PatientInitials_TextBox.Text = AtEditPageLoadPatientReader.GetString(0)
                            AtEditPageLoad_PatientInitials_HiddenField.Value = AtEditPageLoadPatientReader.GetString(0)
                            PatientYearOfBirth_DropDownList.SelectedValue = AtEditPageLoadPatientReader.GetInt32(1)
                            AtEditPageLoad_PatientYearOfBirthID_HiddenField.Value = AtEditPageLoadPatientReader.GetInt32(1)
                            PatientGender_DropDownList.SelectedValue = AtEditPageLoadPatientReader.GetInt32(2)
                            AtEditPageLoad_PatientGenderID_HiddenField.Value = AtEditPageLoadPatientReader.GetInt32(2)
                        End While
                    Catch ex As Exception
                        Response.Redirect("~/Errors/DatabaseConnectionError.aspx")
                        Exit Sub
                    Finally
                        Connection.Close()
                    End Try
                Else 'If current user does not have the right to edit the current ICSR
                    Title_Label.Text = Lockout_Text
                    ButtonGroup_Div.Visible = False
                    Patient_Table.Visible = False
                End If
            Else
                Response.Redirect("~/Login.aspx?ReturnUrl=" & VirtualPathUtility.ToAbsolute("~/") & "Application/EditPatient.aspx?ICSRID=" & CurrentICSR_ID)
            End If
        End If
    End Sub

    Protected Sub Cancel_Button_Click() Handles Cancel_Button.Click
        Dim CurrentICSR_ID As Integer = ICSRID_HiddenField.Value
        Response.Redirect("~/Application/ICSROverview.aspx?ICSRID=" & CurrentICSR_ID)
    End Sub

    Protected Sub ReturnToICSROverview_Button_Click() Handles ReturnToICSROverview_Button.Click
        Dim CurrentICSR_ID As Integer = ICSRID_HiddenField.Value
        Response.Redirect("~/Application/ICSROverview.aspx?ICSRID=" & CurrentICSR_ID)
    End Sub

    Protected Sub PatientInitials_TextBox_Validator_ServerValidate(source As Object, args As ServerValidateEventArgs)
        PatientInitials_TextBox.CssClass = "form-control alert-success"
        PatientInitials_TextBox.ToolTip = String.Empty
        args.IsValid = True
    End Sub

    Protected Sub PatientYearOfBirth_DropDownList_Validator_ServerValidate(source As Object, args As ServerValidateEventArgs)
        PatientYearOfBirth_DropDownList.CssClass = "form-control alert-success"
        PatientYearOfBirth_DropDownList.ToolTip = String.Empty
        args.IsValid = True
    End Sub

    Protected Sub PatientGender_DropDownList_Validator_ServerValidate(source As Object, args As ServerValidateEventArgs)
        PatientGender_DropDownList.CssClass = "form-control alert-success"
        PatientGender_DropDownList.ToolTip = String.Empty
        args.IsValid = True
    End Sub

    Protected Sub SaveUpdates_Button_Click(sender As Object, e As EventArgs) Handles SaveUpdates_Button.Click
        If Page.IsValid = True Then
            Dim CurrentICSR_ID As Integer = ICSRID_HiddenField.Value
            'Store values as present in database when edit page was loaded in variables
            Dim AtEditPageLoad_PatientInitials As String = TryString(AtEditPageLoad_PatientInitials_HiddenField.Value)
            Dim AtEditPageLoad_PatientYearOfBirth_ID As Integer = TryInteger(AtEditPageLoad_PatientYearOfBirthID_HiddenField.Value)
            Dim AtEditPageLoad_PatientGender_ID As Integer = TryInteger(AtEditPageLoad_PatientGenderID_HiddenField.Value)
            'Store values as present in database when save button is clicked in variables
            Dim AtSaveButtonClickReadCommand As New SqlCommand("SELECT Patient_Initials, Patient_YearOfBirth_ID, Patient_Gender_ID FROM ICSRs WHERE ID = @CurrentICSR_ID", Connection)
            AtSaveButtonClickReadCommand.Parameters.AddWithValue("@CurrentICSR_ID", CurrentICSR_ID)
            Dim AtSaveButtonClick_PatientInitials As String = String.Empty
            Dim AtSaveButtonClick_PatientYearOfBirth_ID As Integer = Nothing
            Dim AtSaveButtonClick_PatientGender_ID As Integer = Nothing
            Try
                Connection.Open()
                Dim AtSaveButtonClickReader As SqlDataReader = AtSaveButtonClickReadCommand.ExecuteReader()
                While AtSaveButtonClickReader.Read()
                    AtSaveButtonClick_PatientInitials = AtSaveButtonClickReader.GetString(0)
                    AtSaveButtonClick_PatientYearOfBirth_ID = AtSaveButtonClickReader.GetInt32(1)
                    AtSaveButtonClick_PatientGender_ID = AtSaveButtonClickReader.GetInt32(2)
                End While
            Catch ex As Exception
                Response.Redirect("~/Errors/DatabaseConnectionError.aspx")
                Exit Sub
            Finally
                Connection.Close()
            End Try
            'Check for discrepancies between database contents as present when edit page was loaded and database contents as present when save button is clicked
            Dim DiscrepancyString As String = String.Empty
            DiscrepancyString += DiscrepancyCheck(AtEditPageLoad_PatientInitials, AtSaveButtonClick_PatientInitials, "Patient Initials")
            DiscrepancyString += DiscrepancyCheck(AtEditPageLoad_PatientYearOfBirth_ID, AtSaveButtonClick_PatientYearOfBirth_ID, "Patient Year of Birth")
            DiscrepancyString += DiscrepancyCheck(AtEditPageLoad_PatientGender_ID, AtSaveButtonClick_PatientGender_ID, "Patient Gender")
            'If Discprepancies between database contents as present when edit page was loaded and database contents as present when save button is clicked are found, show warning and abort update
            If DiscrepancyString <> String.Empty Then
                AtSaveButtonClickButtonsFormat(Status_Label, SaveUpdates_Button, Nothing, Cancel_Button, ReturnToICSROverview_Button)
                Status_Label.Style.Add("text-align", "left")
                Status_Label.Style.Add("height", "auto")
                Status_Label.Text = DiscrepancyStringIntro & DiscrepancyString & DiscrepancyStringOutro
                Status_Label.CssClass = "form-control alert-danger"
                PatientInitials_Row.Visible = True
                PatientInitials_TextBox.ReadOnly = True
                PatientInitials_TextBox.ToolTip = String.Empty
                PatientInitials_TextBox.CssClass = "form-control"
                PatientYearOfBirth_Row.Visible = True
                PatientYearOfBirth_DropDownList.Enabled = False
                PatientYearOfBirth_DropDownList.ToolTip = String.Empty
                PatientYearOfBirth_DropDownList.CssClass = "form-control"
                PatientGender_Row.Visible = True
                PatientGender_DropDownList.Enabled = False
                PatientGender_DropDownList.ToolTip = String.Empty
                PatientGender_DropDownList.CssClass = "form-control"
                Exit Sub
            End If
            'If no discrepancies were found between database contents as present when edit page was loaded and database contents as present when save button is clicked, write updates to database
            Dim UpdateCommand As New SqlCommand("Update ICSRs SET Patient_Initials = (CASE WHEN @Patient_Initials ='' THEN NULL ELSE @Patient_Initials END), Patient_YearOfBirth_ID = (CASE WHEN @Patient_YearOfBirth_ID = 0 THEN NULL ELSE @Patient_YearOfBirth_ID END), Patient_Gender_ID = (CASE WHEN @Patient_Gender_ID = 0 THEN NULL ELSE @Patient_Gender_ID END) WHERE ID = @CurrentICSR_ID", Connection)
            UpdateCommand.Parameters.AddWithValue("@Patient_Initials", PatientInitials_TextBox.Text.Trim)
            UpdateCommand.Parameters.AddWithValue("@Patient_YearOfBirth_ID", PatientYearOfBirth_DropDownList.SelectedValue)
            UpdateCommand.Parameters.AddWithValue("@Patient_Gender_ID", PatientGender_DropDownList.SelectedValue)
            UpdateCommand.Parameters.AddWithValue("@CurrentICSR_ID", CurrentICSR_ID)
            Try
                Connection.Open()
                UpdateCommand.ExecuteNonQuery()
                Connection.Close()
            Catch ex As Exception
                Connection.Close()
                Response.Redirect("~/Errors/DatabaseConnectionError.aspx")
                Exit Sub
            End Try
            'Read new patient values from database and store in variables
            Dim UpdatedReadCommand As New SqlCommand("SELECT CASE WHEN Patient_Initials IS NULL THEN '' ELSE Patient_Initials END AS Patient_Initials, CASE WHEN Patient_YearOfBirth_ID IS NULL THEN 0 ELSE Patient_YearOfBirth_ID END AS Patient_YearOfBirth_ID, CASE WHEN Patient_Gender_ID IS NULL THEN 0 ELSE Patient_Gender_ID END AS Patient_Gender_ID FROM ICSRs WHERE ID = @CurrentICSR_ID", Connection)
            UpdatedReadCommand.Parameters.AddWithValue("@CurrentICSR_ID", CurrentICSR_ID)
            Dim Updated_PatientInitials As String = String.Empty
            Dim Updated_PatientYearOfBirth_ID As Integer = Nothing
            Dim Updated_PatientGender_ID As Integer = Nothing
            Try
                Connection.Open()
                Dim UpdatedReader As SqlDataReader = UpdatedReadCommand.ExecuteReader()
                While UpdatedReader.Read()
                    Updated_PatientInitials = UpdatedReader.GetString(0)
                    Updated_PatientYearOfBirth_ID = UpdatedReader.GetInt32(1)
                    Updated_PatientGender_ID = UpdatedReader.GetInt32(2)
                End While
            Catch ex As Exception
                Response.Redirect("~/Errors/DatabaseConnectionError.aspx")
                Exit Sub
            Finally
                Connection.Close()
            End Try
            'Compare old and new patient values to generate EntryString for Case History Entry
            Dim EntryString As String = String.Empty
            EntryString = HistoryDatabasebUpdateIntro
            EntryString += HistoryEnrtyPlainValue("ICSR", CurrentICSR_ID, "Patient Initials", AtSaveButtonClick_PatientInitials, Updated_PatientInitials)
            EntryString += HistoryEntryReferencedValue("ICSR", CurrentICSR_ID, "Patient Year of Birth", tables.YearsOfBirth, fields.Name, AtSaveButtonClick_PatientYearOfBirth_ID, Updated_PatientYearOfBirth_ID)
            EntryString += HistoryEntryReferencedValue("ICSR", CurrentICSR_ID, "Patient Gender", tables.Genders, fields.Name, AtSaveButtonClick_PatientGender_ID, Updated_PatientGender_ID)
            EntryString += HistoryDatabasebUpdateOutro
            'Generate History Entry if any data was changed in the database
            If EntryString <> HistoryDatabasebUpdateIntro & HistoryDatabasebUpdateOutro Then
                Dim InsertHistoryEntryCommand As New SqlCommand("INSERT INTO ICSRHistories(ICSR_ID, User_ID, Timepoint, Entry) VALUES (@ICSR_ID, @User_ID, @Timepoint, @Entry)", Connection)
                InsertHistoryEntryCommand.Parameters.AddWithValue("@ICSR_ID", CurrentICSR_ID)
                InsertHistoryEntryCommand.Parameters.AddWithValue("@User_ID", LoggedIn_User_ID)
                InsertHistoryEntryCommand.Parameters.AddWithValue("@Timepoint", Now())
                InsertHistoryEntryCommand.Parameters.AddWithValue("@Entry", EntryString)
                Try
                    Connection.Open()
                    InsertHistoryEntryCommand.ExecuteNonQuery()
                Catch ex As Exception
                    Response.Redirect("~/Errors/DatabaseConnectionError.aspx")
                    Exit Sub
                Finally
                    Connection.Close()
                End Try
            End If
            'Format Controls
            AtSaveButtonClickButtonsFormat(Status_Label, SaveUpdates_Button, Nothing, Cancel_Button, ReturnToICSROverview_Button)
            TextBoxReadOnly(PatientInitials_TextBox)
            DropDownListDisabled(PatientYearOfBirth_DropDownList)
            DropDownListDisabled(PatientGender_DropDownList)
        End If
    End Sub
End Class
